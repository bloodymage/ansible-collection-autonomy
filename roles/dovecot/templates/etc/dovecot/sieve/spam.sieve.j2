# {{ ansible_managed }}

require ["envelope", "subaddress", "fileinto", "imap4flags", "regex", "mailbox", "date"];

# Delete email where spam level is higher than 10
if header :contains "X-Spam-Level" "**********" {
  discard;
  stop;
}

# Junk improperly formed message IDs
if not header :regex "message-id" ".*@.*\\." {
  fileinto "{{ dovecot_junk_mail_folder }}";
  setflag "\\Seen";
  stop;
}

# File low-level spam in spam bucket, and viruses in Infected folder
if anyof (header :contains "X-Spam-Level" "*****",
          header :contains "X-Virus-Status" "Infected") {
  if header :contains "X-Spam-Level" "*****" {
      fileinto "{{ dovecot_junk_mail_folder }}";
      setflag "\\Seen";
      stop;
    }
  else {
    fileinto :create "{{ dovecot_infected_mail_folder }}";
    stop;
  }
}

if anyof (
  envelope :matches :detail "to" "spam",
  header :contains "X-Spam-Flag" "YES",
  header :contains "Authentication-Results" "dkim=fail",
  header :contains "Authentication-Results" "spf=fail",
  header :contains "Authentication-Results" "spf=none"
)
{
  fileinto "{{ dovecot_junk_mail_folder }}";
  setflag "\\Seen";
  # Stop here - if there are other rules, ignore them for spam messages
  stop;
}
