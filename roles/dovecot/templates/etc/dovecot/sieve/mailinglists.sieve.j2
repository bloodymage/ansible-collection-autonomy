# {{ ansible_managed }}

require ["include", "variables", "fileinto", "mailbox", "envelope", "subaddress", "regex", "imap4flags"];
global ["autonomy_category", "autonomy_explicit_group", "autonomy_listname", "autonomy_detail", "autonomy_detected_domain", "autonomy_explicit_subgroup", "autonomy_generic_subgroup"];

if exists "list-id" {
  if anyof (
            header :regex "list-id" "<([a-z_0-9-]+)[.@]",
            header :regex "list-id" "^\\s*<?([a-z_0-9-]+)[.@]"
  ) {
    set :lower :upperfirst "autonomy_listname" "${1}";
  }
}

# Listar and mailman like
elsif exists "x-list-id" {
  if header :regex "x-list-id" "<([a-z_0-9-]+)\\\\." {
    set :lower "autonomy_listname" "${1}";
  }
}

# Ezmlm
elsif exists "mailing-list" {
  if header :regex "mailing-list" "([a-z_0-9-]+)@" {
    set :lower "autonomy_listname" "${1}";
  }
}

# York lists service
elsif exists "x-mailing-list" {
  if header :regex "x-mailing-list" "^\\s*([a-z_0-9-]+)@?" {
    set :lower "autonomy_listname" "${1}";
  }
}

# Smartlist
elsif exists "x-loop" {
  if header :regex "x-loop" "^\\s*(a-z_0-9-]+)@?" {
    set :lower "autonomy_listname" "${1}";
  }
}

# poorly identified
elsif envelope :contains "from" "owner-" {
  if anyof (
            envelope :regex "from" "owner-([a-z_0-9-]+)-outgoing@",
            envelope :regex "from" "owner-([a-z_0-9-]+)@",
            header :regex "Sender" "owner-([a-z_0-9-]+)@"
  ) {
      set :lower "autonomy_listname" "${1}";
  }
}

# other poorly identified
elsif  envelope :contains "from" "-request" {
  if envelope :regex "from" "([a-z_0-9-]+)-request@" {
    set :lower "autonomy_listname" "${1}";
  }
}

if header :matches "X-GitLab-Project" "*"
{
  set :lower :upperfirst "autonomy_listname" "${1}";
}

if header :matches "X-Redmine-Project" "*"
{
  set :lower :upperfirst "autonomy_listname" "${1}";
}
